plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'me.moallemi.advanced-build-version'
    id 'com.adarshr.test-logger' version '3.2.0'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "dev.yasan.metro"
        minSdk 21
        targetSdk 33

        versionCode 235
        versionName "2.3.5"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    def dimenStore = 'store'
    flavorDimensions dimenStore
    productFlavors {
        fdroid {
            dimension dimenStore
            applicationIdSuffix(".fdroid")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'dev.yasan.metro.tehran'
}
advancedVersioning.renameOutputApk()

dependencies {
    // YASAN Development Kit
    implementation 'com.github.yasanglass:ydk-android:3.0.1'

    // Para
    implementation 'com.github.yasanglass:para:0.2.0'

    // Misc
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.7.0'

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation 'androidx.activity:activity-compose:1.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    implementation "androidx.navigation:navigation-compose:2.5.3"

    // Compose Material
    implementation "androidx.compose.material:material:$compose_material_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_material_version"

    // Accompanist
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-material:$accompanist_version"

    // Lifecycle
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Leak Canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'

    // Subsampling Scale ImageView
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'

    // Coil
    implementation("io.coil-kt:coil-compose:$coil_version")

    // Persian Date
    implementation 'com.github.samanzamani:PersianDate:1.5.4'

    // Local Unit Tests
    implementation 'androidx.test:core:1.5.0'
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation 'org.robolectric:robolectric:4.9.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation "com.google.truth:truth:$truth_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"

    // Instrumented Unit Tests
    androidTestImplementation "junit:junit:$junit_version"
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "com.google.truth:truth:$truth_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"

}